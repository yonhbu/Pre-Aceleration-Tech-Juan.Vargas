allprojects {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    compileJava.dependsOn validateStructure
    sourceCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor  "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly  "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor  "org.projectlombok:lombok:${lombokVersion}"
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
    
    
    ext.libs = [
            unit_tests: [
                    "com.squareup.okhttp3:okhttp:4.2.2",
                    "com.squareup.okhttp3:mockwebserver:4.2.2",
                    "io.projectreactor:reactor-test:3.3.0.RELEASE",
                    "org.springframework.boot:spring-boot-starter-test:${springBootVersion}",
            ],
            acceptance_tests: [
                    "com.googlecode.yatspec:yatspec:1.20"
            ],
            security: [
                    "org.springframework.boot:spring-boot-starter-security"
            ],
            integration_test: [
                    "org.springframework:spring-test:${springBootVersion}",
                    "org.skyscreamer:jsonassert:1.3.0",
                    "com.cedarsoftware:json-io:4.4.0"
            ],
            end_to_end_t: [
                    "org.springframework:spring-test:${springBootVersion}",
                    "com.mashape.unirest:unirest-java:1.4.8",
                    "org.skyscreamer:jsonassert:1.3.0",
                    "com.cedarsoftware:json-io:4.4.0"
            ],
            spring_boot: [
                    "org.springframework.boot:spring-boot-starter-web:${springBootVersion}",
                    "org.springframework:spring-context"
            ],
            spring_rest: [
                    "org.springframework.boot:spring-boot-starter-logging:${springBootVersion}",
                    "org.springframework.boot:spring-boot-devtools:${springBootVersion}",
                    "org.springframework.boot:spring-boot-starter-actuator"
            ],
            spring_security: [
                    "org.springframework.boot:spring-boot-starter-security"
            ],
            spring_jpa: [
                    "org.springframework.boot:spring-boot-starter-data-jpa"
            ],
            jwt: [
                    "io.jsonwebtoken:jjwt:0.9.1"
            ],
            string_utils: [
                    "org.apache.commons:commons-lang3:3.4",
                    "commons-io:commons-io:2.6"
            ],
            jack:[
                    "com.fasterxml.jackson.core:jackson-annotations:2.13.0"
            ],
            config_server: [
                    "org.springframework.retry:spring-retry",
                    "org.springframework.cloud:spring-cloud-starter-config"
            ],
            api_validation:[
                    "javax.validation:validation-api:2.0.1.Final"
            ],
            entry_point:[
                    "com.fasterxml.jackson.core:jackson-databind:2.10.3",
                    "javax.el:javax.el-api:3.0.0",
                    "org.glassfish.web:javax.el:2.2.1",
                    "org.apache.commons:commons-text:1.8"
            ],
            log:[
                    "org.slf4j:slf4j-api:1.7.19"
            ],  
            mapper:[
                    "org.modelmapper:modelmapper:2.1.1"
            ], 
            mysql:[
                    "mysql:mysql-connector-java"
            ], 
            test_unit:[
                    "org.powermock:powermock-api-mockito2:1.7.4",
                    "org.mockito:mockito-core:2.8.9",
                    "org.powermock:powermock-module-junit4:1.6.4",
                    "junit:junit:4.12"
            ]
    ]

    test.finalizedBy(project.tasks.jacocoTestReport)

    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            xml.destination file("${buildDir}/reports/jacoco.xml")
            csv.enabled false
            html.destination file("${buildDir}/reports/jacocoHtml")
        }
    }

}

jacoco {
    toolVersion = "${jacocoVersion}"
    reportsDir = file("$buildDir/reports")
}

task jacocoMergedReport(type: JacocoReport) {
    dependsOn = subprojects.jacocoTestReport
    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom project.fileTree(dir: '.', include: '**/build/jacoco/test.exec')
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [
            '-Amapstruct.suppressGeneratorTimestamp=true'
    ]
}
tasks.named('wrapper') {
  gradleVersion = '6.7'
 }
